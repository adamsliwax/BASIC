10 PRINT "HEX DUMP (256 bytes) >> Z-80 PLUMCO <<"
20 LET J=0
30 LET MAXLIN=16
40 LET MAXBAJT=&H00FF
50 LET BASE=&HF000
60 REM --- VARIABELS --------------
70 H$=""
80 INPUT "ENTER ADDRESS OF START MEM :";H$
90 GOSUB 400
95 A=T
100 IF T>32767 THEN A=T-65536
105 H$=""
110 PRINT HEX$(A+1);" :";
120 REM----MAIN LOOP ----------------
130 FOR I=1 TO MAXBAJT
140 LET D=PEEK(A+I)
150 IF (D>33)*(D<127) THEN H$=H$+CHR$(D)
160 IF (D<33)+(D>127) THEN H$=H$+"."
170 IF D<16 THEN PRINT "0";
180 PRINT HEX$(D);
190 PRINT ",";
200 LET J=J+1
210 IF J=16 THEN GOSUB 300
220 NEXT I
230 PRINT
240 END
300 REM  --- NEXT LINE ------------------------------------------
310 PRINT "   ";H$
320 H$=""
330 PRINT HEX$(A+I);" :";
340 LET J=0
350 RETURN
400 REM --- HEX TO DEC ------------------------------------------
420 T=0:D=1
430 FOR P=LEN(H$)-1 TO 0 STEP -1
440 C=ASC(MID$(H$,D,1))
450 D=D+1
460 IF (C>=48)*(C<=57) THEN C=C-48:GOTO 490
470 IF (C>=65)*(C<=70) THEN C=C-55:GOTO 490
480 PRINT "BLAD":END
490 T=T+C*16^P
500 NEXT P
510 PRINT "HEX ";H$;" IN DECIMAL IS ";T
520 RETURN
